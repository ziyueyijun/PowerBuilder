# 窗口

## 常用

```c
//数据库连接与断开
connect using sqlca; //连接
disconnect using sqlca; //断开

//事务提交与回滚
commit using sqlca;  //提交
rollback using sqlca; //回滚

//创建与销毁对象
create //创建一个对象
destroy //销毁一个对象
if IsValid(对象名称) then destroy 对象名称 //如果对象存在则销毁对象

//给变量分配空间
string ls_test
ls_test =  space(1024)
    
//定位断点位置
debugbreak()
    
//获取当前项目路径
getcurrentdirectory()
    
//读、写 ini 文件
profilestring(getcurrentdirectory() + "\pb.ini","pb","abc","我是备用的默认值")
setProfilestring(getcurrentdirectory() + "\pb.ini","pb","abc","我是要写入的参数")
```

## 参数

### 字符串

```c
// 1、传递
openwithparm( 新窗口名称 , '1' )
//closewithreturn( 当前窗口名称 , '1' )

// 2、接收
string  ls_str 
ls_str = message.stringparm
messagebox('提示',ls_str);
```

### 数值

```c
// 1、传递
openwithparm( 新窗口名称 , 1 )
//closewithreturn( 当前窗口名称 , 1 )

// 2、接收
double  ldb_double
ldb_double = message.doubleparm
messagebox('提示',ldb_double);
```

### 结构体

```c
// 1、传递
st_parameter s_pm
s_pm.a = 1
openwithparm( 新窗口名称 , s_pm )
//closewithreturn( 当前窗口名称 , s_pm ) 

// 2、新窗口 ——> 接收
st_parameter s_pm 
s_pm =  message.powerobjectparm
messagebox('提示',s_pm.a)
```

### 用户对象

```c
// 1、传递
u_parameter  u_pm
u_pm = create u_parameter
u_pm.a = '1'
openwithparm( 新窗口名称 , u_pm )

// 2、接收
u_parameter u_pm 
u_pm =  message.powerobjectparm
messagebox('提示',u_pm.a)
```

### 用户对象（ 可视化 ）

```c
u_customvisual u_cv
u_cv = create  u_customvisual
openuserobjectwithparm(u_cv,1)
```

## 游标

### 用  for  循环实现游标

```c
long ll_count = 10

//定义游标
string test

//声明游标
declare test cursor for 

// SQL 语句
select 字段名 from  表名 where 表达式 using sqlca; 

//打开游标
open test; 

for i=1 to ll_count
 //获取数据
 fetch test into :变量名; 
 
 //在这里写你的业务
 
next

//关闭游标
close test;
```

### 用 do while  循环实现游标

```c
//定义游标
string test 

//声明游标
declare test cursor for 
  
// SQL语句
select 字段名 from 表名 where 条件 using sqlca;

//打开游标
open test;

//获取数据
fetch test into:字段名变量;

do while sqlca.sqlcode = 0

 //在这里写你的业务
 
 //再次获取数据
 fetch test into:字段名变量; 
 
loop

//关闭游标
close test;
```

## SQL

### 立即执行

```c
string ls_sql,ls_err
ls_sql = "update 表名 set 字段名 =:字段值 where 表达式条件"
//执行SQL语句
execute immediate :ls_sql using sqlca;
if sqlca.sqlcode = 0 then
    commit using sqlca;
    messagebox('提示','执行成功')
else
    ls_err = sqlca.sqlerrtext
    rollback using sqlca;
    messagebox('提示','执行失败' + ls_err)
end if
```

### 预处理

```c
long ll_id
string ls_name
ls_sql = "select 字段名 from 表名 where id=? "
ll_id= 10
//定义动态SQL
declare test dynamic cursor for sqlsa;
//准备要执行的SQL
prepare sqlsa from :ls_sql;
//打开动态SQL语句，并将 ll_id 作为参数执行
open dynamic test using :ll_id;
//获取SQL语句的执行结果
fetch test into :ls_name;
//关闭动态SQL
close test;
```

### 动态执行

```c
string ls_sql
ls_sql = "select count(*) from 表名 where id = 10 "
//定义动态SQL
declare test dynamic cursor for sqlsa;
//准备要执行的SQL
prepare sqlsa from :ls_sql;
//打开动态SQL语句
open dynamic test;
//执行动态SQL语句，并获取返回值
fetch test into :ll_state;
//关闭动态SQL
close test;
```

### 返回最后插入到自增列中的值

```sql
string ls_sql
longlong ll_id
ls_sql = "select SCOPE_IDENTITY();"
declare test dynamic cursor for sqlsa;
prepare sqlsa from :ls_sql;
open dynamic test;
fetch test into :ll_id;
close test;
```

## 浏览器

### 打开一个网页

```c
Inet iinet_base
GetContextService("Internet", iinet_base)
iinet_base.HyperlinkToURL('www.baidu.com')
```

### 通过默认浏览器打开一个网页

```c
//声明函数
function long shRunDefltBrowser(string szUrl) library "pbvm90.dll"
//调用函数
shRunDefltBrowser("www.baidu.com")
```

## 存储过程

1、创建存储过程

> 在 SqlServer 数据库中先创建存储过程

```sql
create procedure sp_test
 @param varchar(30)='',
 @refcode int output,
 @refmsg varchar(50) output
as

/*
存储过程：sp_test
功能：PowerBuilder 调用测试
入参：
	@param 传入的参数
出参：
	@refcode 返回状态码
	@refmsg 返回的提示信息
*/

-- 返回的状态码
set @refcode = 200
--返回的提示信息
set @refmsg = '请求成功'
--返回的结果集
select id,name from (
	select 1 as id,'张三' name
	union
	select 2 as id,'李四' name
) tab
```

2、调用存储过程

> 在 PowerBuilder 中调用存储过程

```c
string test,ls_param,ls_refmsg
long row = 0,li_refcode

ls_param = '我是存储过程入参';

//1、声明存储过程实例
declare test  procedure for sp_test
	@param = :ls_param,
	@refcode = :li_refcode output,
	@refmsg = :ls_refmsg  output
	using sqlca;

//2、执行存储过程实例
execute test;

if sqlca.sqlcode = 0 then
	
	//3、接收结果集
	string ls_id,ls_name
	do
		fetch test  into:ls_id,:ls_name;
		if  sqlca.sqlcode = 0 then
			row ++
		elseif sqlca.sqlcode = 100 then
			messagebox ("提示","接收结果集完成，共" + string(row) + ' 条数据!')
			goto parm
		else
			rollback using sqlca;
			messagebox ("提示","接收结果集失败~r~n" + string (sqlca.sqldbcode) + " = " + sqlca.sqlerrtext)
             return -1
		end if
	loop while sqlca.sqlcode = 0
	
	parm:
	
	//4、接收返回参数
	fetch test  into:li_refcode,:ls_refmsg;
	if  sqlca.sqlcode = 0 then
		messagebox ("提示","返回的状态码为：" + string(li_refcode) + ",返回的提示信息为：" + ls_refmsg )
	elseif sqlca.sqlcode = 100 then
		messagebox ("提示", "调用存储过程没有返回接收参数")
        return -1
	else
		rollback using sqlca;
		messagebox ("提示","接收返回参数失败~r~n" + string (sqlca.sqldbcode) + " = " + sqlca.sqlerrtext)
         return -1
	end if
	
	close test;
	
elseif sqlca.sqlcode = 100 then
	messagebox ("提示", "调用存储过程没有返回结果集")
    return -1
else
	rollback using sqlca;
	messagebox ("调用存储过程失败",string (sqlca.sqldbcode) + " = " + sqlca.sqlerrtext)
    return -1
end if
    
return 0
```

## 错误处理

在当前应用（ Application ）的 systemerror 事件中，添加以下代码

```c
string ls_errmsg
ls_errmsg = "系统错误:~r~n"
ls_errmsg += "错误号: " + string(error.number) + ";~r~n"
ls_errmsg += "错误信息: " + string(error.text) + ";~r~n"
ls_errmsg += "出错对象: " + string(error.object) + ";~r~n"
ls_errmsg += "出错事件: " + string(error.objectevent) + ";~r~n"
ls_errmsg += "出错行: " + string(error.line) + ";~r~n"
messagebox('系统错误',ls_errmsg)
```

## 弹窗

语法：messagebox(标题,内容,左侧的图标,底部的按钮,底部的按钮编号)

```c
//左侧图标可选的枚举值
information!	【提醒：蓝色叹号 ！】	默认值
stopsign!		【危险：红色 x 】
exclamation!	【警告：黄色叹号 ！】		
question!		【疑问：蓝色问号 ？】
None!			【不显示任何图标】

//弹窗底部的按钮
ok!					//【"确定"】
okcancel!			//【"确定" or "取消"】			
yesno!				//【"是" or "否"】
yesnocancel!		//【"是" or "否" or "取消"】
retrycancel!		//【"重试" or "取消"】
abortretryignore!	//【"中止" or "重试" or "忽略"】
    
//弹窗底部的按钮编号，可以设置打开弹窗时，焦点在哪个按钮上
 
//例子（弹窗，并让用户 "是" 或者 "否" 选项，默认弹窗底部的按钮编号为 2 = "否"，防止用户误操作）
if messagebox('提示','是否执行此操作?',question!,yesno!,2) = 1 then
 messagebox('提示','是')
else
 messagebox('提示','否')
end if
```

## 开发模式下显示当前窗口名称

```c
//可以写到 open 事件，打开窗口，窗口标题就显示当前窗口文件名
if handle(getapplication()) = 0 then
	this.title = this.title + '(' + this.classname() + ')'
end if
```

## 获取各种时间

```c
//获取当前操作系统日期
datetime ldt_dqsj 
ldt_dqsj = datetime(today(),now())

//datetime 转 string ( 完整格式 )
string(ldt_dqsj,"yyyy-mm-dd hh:mm:ss")
//datetime 转 string ( 仅显示 date )
string(ldt_dqsj,"yyyy-mm-dd")
//datetime 转 string ( 仅显示 time )
string(ldt_dqsj,"hh:mm:ss")
    
//将 string 格式转为 datetime 格式
string ls_datetime,ls_rq,ls_sj
ls_datetime = string(ldt_dqsj)
ls_rq =  mid(ls_datetime,1,10)
ls_sj = mid(ls_datetime,12,21)
messagebox('提示',string(datetime(date(ls_rq),time(ls_sj))
          
//将 date 和 time 转换为 datetiem 格式
date lde_rq
time ltm_sj
lde_rq = 2022-04-15
ltm_sj = 10:10:10
messagebox('提示',string(datetime(lde_rq,ltm_sj)))

//获取 0 点到 23:59:59 秒的时间
em_kssj.text =  string(datetime(today(),time("00:00:00")))
em_jssj.text =  string(datetime(today(),time("23:59:59")))
或者
em_kssj.text = string(datetime(today(), 00:00:00))
em_jssj.text =  string(datetime(today(),23:59:59))
```

## 窗口控制

```c
//新增事件：ue_window_action
//事件ID为：pbm_syscommand 
//在当前事件中写入以下代码
string ls_msg
choose case commandtype
	case 61472
		ls_msg = '您点击了最小化按钮'
	case 61728
		ls_msg = '您恢复了最小化窗口'
	case 61488
		ls_msg = '您点击了最大化按钮'
	case 61536
		ls_msg = '您点击了关闭按钮'
	case 61458
		ls_msg = '您用鼠标左键单击了窗口'
	case 61490
		ls_msg = '您用鼠标左键双击了窗口，使其最大化'
	case 61730
		ls_msg = '您用鼠标左键双击了窗口，恢复默认大小'
	case 61587
		ls_msg = '您用鼠标左键单击了窗口最左侧的控制菜单'
	case 61456
		ls_msg = '您选择了控制菜单下的【移动】选项'
	case 61441
		ls_msg = '您点击了窗口左边框'
	case 61442
		ls_msg = '您点击了窗口右边框'
	case 61443
		ls_msg = '您点击了窗口顶部边框'
	case 61446
		ls_msg = '您点击了窗口底部边框'
	case 61444
		ls_msg = '您点击了窗口左上角边框'
	case 61445
		ls_msg = '您点击了窗口右上角边框'
	case 61447
		ls_msg = '您点击了窗口左下角边框'
	case 61448
		ls_msg = '您点击了窗口右下角边框'
end choose

mle_1.text += string(commandtype) + '：' + ls_msg + '~r~n'
//return 0(继续操作) 1(终止操作)
return 0
```

## 窗口大小

```c
resize事件! 
参数:sizetype 整型 (0 正常显示,1 最小化,2 最大化) 
newwidth 新宽度 
newheigh 新高度
return 1
```

## 菜单

### 分隔符

```c
使用破折号（ —— ）来作为菜单的分割符，并规范命名为 m_行号
```

### 加速键

```c
在菜单的 text 名称里可以定义加速键（方便快速查找），格式为(&字母)
比如定义菜单 open 修改为 open(&o) 这样在输入 o 的时候就能快速的找到这个菜单了
不过这样也会导致一个问题，当按 alter + o 键时，会触发点击事件
这样就会与我们的菜单快捷设置的 ctrl or shift or alt 键产生冲突
建议使用 2 位数及以上的加速键，例如 open(&op)，这样可避免与菜单快捷键冲突
```

### 快捷键

```c
可通过 shortcut key 来选择要触发的快捷键
并且可以选择与 shortcut alt 或者 shortcut ctrl 或者 shortcut shift 键来配合使用
```

### 右键弹出菜单

```c
//先创建菜单控件(menu)并命名为 m_main、

//声明菜单对象 m_main
m_main m_test

//创建菜单实例 
m_test = create m_main

//在鼠标光标处显示弹出菜单
m_test.popmenu(this.pointerx(),this.pointery())
    
//多级菜单的使用
m_test1.m_test2.m_test3.popmenu(this.pointerx(),this.pointery())
```

## 快捷键

> 在 key 事件中写入以下代码

### 功能键

```c
//F1 ~ F12
if KeyDown(KeyF1!) then
	messagebox('提示','您按下了【F1】键')
end if
```

### 字母键

```c
//A-Z（不区分大小写）
if KeyDown(KeyA!) then
	messagebox('提示','您按下了【A】键')
end if
```

### 数字和符号键

```c
//0-9（非小键盘）
if KeyDown(Key0!) then
	messagebox('提示','您按下了【0】键')
end if
```

### 数字键

```c
//0-9（小键盘）
if KeyDown(KeyNumpad0!) then
	messagebox('提示','您按下了【0】键')
end if
```

### 小键盘符号键

```c
//乘号键（*）
if KeyDown(KeyMultiply!) then
	messagebox('提示','您按下了【*】键')
end if

//加号键（+）
if KeyDown(KeyAdd!) then
	messagebox('提示','您按下了【+】键')
end if
    
//减号键（-）
if KeyDown(KeySubtract!) then
	messagebox('提示','您按下了【-】键')
end if
 
//小数点键（.）
if KeyDown(KeyDecimal!) then
	messagebox('提示','您按下了【.】键')
end if
    
//除号键（/）
if KeyDown(KeyDivide!) then
	messagebox('提示','您按下了【/】键')
end if

//锁定键（NumLock）
if KeyDown(KeyNumLock!) then
	messagebox('提示','您按下了【NumLock】键')
end if
```

### 控制键

```c
//换挡键（shift）
if KeyDown(KeyShift!) then
	messagebox('提示','您按下了【Shift】键')
end if

//控制键（ctrl）
if KeyDown(KeyControl!) then
	messagebox('提示','您按下了【ctrl】键')
end if

//转换键（alt）
if KeyDown(KeyAlt!) then
	messagebox('提示','您按下了【ctrl】+【alt】键')
end if
    
//暂停键（pause/break）
if KeyDown(KeyPause!) then
	messagebox('提示','您按下了【pause/break】键')
end if
    
//大小写切换键（CapsLock）
if KeyDown(KeyCapsLock!) then
	messagebox('提示','您按下了【CapsLock】键')
end if
    
//返回键（ESC）
if KeyDown(KeyEscape!) then
	messagebox('提示','您按下了【ESC】键')
end if
    
//截屏键（PrintScreen/sysrq）
if KeyDown(KeyPrintScreen!) then
	messagebox('提示','您按下了【PrintScreen/sysrq】+【shift/ctrl】键')
end if
    
//插入键（ins）
if KeyDown(KeyInsert!) then
	messagebox('提示','您按下了【ins】键')
end if
    
//删除键（del）
if KeyDown(KeyDelete!) then
	messagebox('提示','您按下了【del】键')
end if
```

### 导航键

```c
//向上翻页键（PageUp）
if KeyDown(KeyPageUp!) then
	messagebox('提示','您按下了【PageUp】键')
end if
    
//向下翻页键（PageDown）
if KeyDown(KeyPageDown!) then
	messagebox('提示','您按下了【PageDown】键')
end if
    
//结束键（end）
if KeyDown(KeyEnd!) then
	messagebox('提示','您按下了【end】键')
end if

//置顶/行首键
if KeyDown(KeyHome!) then
	messagebox('提示','您按下了【home】键')
end if
    
//方向键（左）
if KeyDown(KeyLeftArrow!) then
	messagebox('提示','您按下了【←】键')
end if
    
//方向键（上）
if KeyDown(KeyUpArrow!) then
	messagebox('提示','您按下了【↑】键')
end if
    
//方向键（右）
if KeyDown(KeyRightArrow!) then
	messagebox('提示','您按下了【→】键')
end if
    
//方向键（下）
if KeyDown(KeyDownArrow!) then
	messagebox('提示','您按下了【↓】键')
end if

//滚动锁定键（ScrollLock）
if KeyDown(KeyScrollLock!) then
	messagebox('提示','您按下了【ScrollLock】键')
end if
```

### 其他按键

```c
//退格键（←）
if KeyDown(KeyBack!) then
	messagebox('提示','您按下了【退格】键')
end if
    
//控制键（ctrl） + 制表符键（tab）
if KeyDown(KeyTab!) then
	messagebox('提示','您按下了【ctrl】 + 【tab】键')
end if

//回车键（Enter）
if KeyDown(KeyEnter!) then
	messagebox('提示','您按下了【Enter】键')
end if

//空格键（SpaceBar）
if KeyDown(KeySpaceBar!) then
	messagebox('提示','您按下了【空格键】键')
end if
    
//单、双引号键（' and "）
if KeyDown(KeyQuote!) then
	messagebox('提示','您按下了按键~r~n' +&
	'" 和 ' +&	
	"'")
end if

//加号（+）和等于号（=）
if KeyDown(KeyEqual!) then
	messagebox('提示','您按下了按键~r~n' +&
	"+ 和 " +&	
	"=")
end if
    
//逗号（,）和小于号（<）
if KeyDown(KeyComma!) then
	messagebox('提示','您按下了按键~r~n' +&
	", 和 " +&	
	"<")
end if

//破折号（——）和下划线（_）
if KeyDown(KeyDash!) then
	messagebox('提示','您按下了按键~r~n' +&
	"—— 和 " +&	
	"_")
end if
    
//小数点（.）和大于号（>）
if KeyDown(KeyPeriod!) then
	messagebox('提示','您按下了按键~r~n' +&
	". 和 " +&	
	">")
end if
    
//斜杠（/）和问号（?）
if KeyDown(KeySlash!) then
	messagebox('提示','您按下了按键~r~n' +&
	"/ 和 " +&	
	"?")
end if
    
//反引号（`）和波浪号（~）
if KeyDown(KeyBackQuote!) then
	messagebox('提示','您按下了按键~r~n' +&
	"` 和 " +&	
	"~~")
end if
    
//中括号（[）和大括号（{）
if KeyDown(KeyLeftBracket!) then
	messagebox('提示','您按下了按键~r~n' +&
	"[ 和 " +&	
	"{")
end if
    
//反斜杠（\）和竖线（|）
if KeyDown(KeyBackSlash!) then
	messagebox('提示','您按下了按键~r~n' +&
	"\ 和 " +&	
	"|")
end if
    
//右中括号（]）和右大括号（}）
if KeyDown(KeyRightBracket!) then
	messagebox('提示','您按下了按键~r~n' +&
	"] 和 " +&	
	"}")
end if
    
//冒号（:）和分号（;）
if KeyDown(KeySemiColon!) then
	messagebox('提示','您按下了按键~r~n' +&
	": 和 " +&	
	";")
end if
```

## 设置鼠标形状

> 可以使用 SetPointer 函数来设置鼠标形状

```c
long row
for row = 1 to 5000000
	SetPointer(AppStarting!) //箭头+圆圈
	//SetPointer(Arrow!) //箭头
	//SetPointer(Cross!) //十字
	//SetPointer(Beam!) //工型
	//SetPointer(Help!) //箭头+问号
	//SetPointer(HourGlass!) //圆圈
	//SetPointer(Hyperlink!) //手指
	//SetPointer(Icon!) //图标
	//SetPointer(Size!) //大小
	//SetPointer(SizeNS!) //大小-上下
	//SetPointer(SizeNESW!) //大小-下左右上
	//SetPointer(SizeWE!) //大小-左右
	//SetPointer(SizeNWSE!) //大小-左上右下
	//SetPointer(UpArrow!) //上箭头
next
```

## 计算执行时间

```c
long row,s
s = cpu()
for row = 1 to 5000000
	//
next
s = cpu() - s
messagebox("提示","本次循环共花费了" + string(s / 1000) + " 秒")
```

## 通信

### 当前程序

```c
//1、传递 string 消息
w_main.triggerevent('ue_open',0,'test')
//在用户自定义事件 ue_open 中接收消息：
string ls_msg
ls_msg = string(message.longparm,'address')

//2、传递 long 消息
w_main.triggerevent('ue_open',100,0)
//在用户自定义事件 ue_open 中接收消息：
long ll_msg
ll_msg = message.wordparm
```

### 新程序

```c
// 1、当前程序 ——> 传递到新的应用程序
ls_exe = 'a.exe' + '|值1|' + '|值2|'
run(ls_exe)

// 2、新的应用程序 ——> 接收
string ls_parameter 
ls_parameter = commandparm()
```

## 控件

### tab

#### 修改 tab 控件名称

```c
tab_1.control[1].text = '我是修改后的名称'
```

修改默认的 tab

```c
tab_1.selecttab(2) 
```



# 其他

## 将当前窗口置顶于其他窗口的前面 

```c
窗口名.BringToTop = TRUE
```

## 定义外部存储过程

```c
function long 存储过程名称(string 存储过程参数,ref long 存储过程返回值) rpcfunc alias for "dbo.存储过程名称" 
```

## 禁止拖动带标题的数据窗口

```c
//在数据窗口事件列表中，新增事件：ue_nchi
//事件ID：pbm_nchittest 
//在脚本区域写入以下代码
return 2
```

## 打开目录

```c
run("explorer 目录路径")
```

## 获取自增列序号

```sql
select top 1 scope_identity() into :ll_res_id from 表名 using sqlca;
```

## 获取鼠标的 x、y 坐标

```c
对象.pointerX()
对象.pointerY()
```

## 读取 text 类型的字段到 string 中

```c
//例1：
// 获取text字段的总长度
select datalength(strmax1) into :ll_totallength from 表名 where id = :ll_id;
// 分段读取
ll_offset = 0
ll_length = 1024 // 每次读取1kb
do while ll_offset < ll_totallength
    select substring(strmax1,:ll_offset,:ll_length) into :ls_text from 表名 where id = :ll_id;
    ls_strmax1 += string(ls_text)
    ll_offset += ll_length
loop
messagebox('提示',ls_strmax1)

//例2：
blob lb_strmax1
selectblob strmax1 into:lb_strmax1 from 表名 WHERE id = :ll_id;
ls_strmax1 = string(lb_strmax1)
messagebox('提示',ls_strmax1)
```

