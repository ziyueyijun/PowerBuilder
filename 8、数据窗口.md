# 数据窗口

## 1、增删改查

### 1.1、接受主缓冲区的值

```c
dw_1.accepttext()
```

### 1.2、增加 数据

```c
dw_1.insertrow(0)
```

### 1.3、删除数据

```c
//例1：删除第 1 行数据
dw_1.deleterow(1)
    
//例2：保留数据窗口第1行，并循环删除其他行
for ll_i = dw_1.rowcount() to 1  step - 1
	if ll_i = 1 then
		dw_1.object.字段名[1] = 值
	else
		dw_1.deleterow(ll_i)
	end if
next
        
//例3：循环删除
//先记录本次需要删除的序号
long ll_delete[],i,row,ll_count
ll_count = dw_1.rowcount()       
for row = 1 to ll_count
	if f_isnull(dw_1.object.字段名[row],0) = 0 then
        ll_delete[i] = row
		i ++
	end if
next
//再循环进行删除
long ll_delete_count
ll_delete_count = upperbound(ll_delete)
for row = 1 to  ll_delete_count
	dw_1.deleterow(ll_delete[ll_delete_count + 1 - row])
next
```

### 1.4、更新数据

#### [1.4.1、更新属性](数据窗口/更新属性.md)

#### 1.4.2、更新实例

```c
string ls_err
if dw_1.update() = 1 then
	commit using sqlca;
	messagebox('提示','更新成功')
else
	ls_err = sqlca.sqlerrtext
	rollback using sqlca;
	messagebox('提示','更新失败' + ls_err)
end if

//重置更新标志
dw_1.ResetUpdate()
```

### 1.5、检索数据

```c
//设置数据窗口控件外部事务对象，必须指定connect,commit,disconnect
dw_1.settransobject(sqlca)
dw_1.retrieve()

//设置数据窗口控件内部事务的值。自动connect,commit,disconnect
dw_1.settrans(sqlca)
dw_1.retrieve()
```

## 2、赋值与取值

```c
//赋值
dw.setitem(行,'列名称',值)
dw.object.列名[行号] = 值

//取值
dw_1.object.字段名(行数) //直接取值
dw_1.object.标题名.text  //标题文本框
dw_1.GetItemString(行数,列名) //字符串
dw_1.GetItemNumber(行数,列名) //数值
dw_1.GetItemDate(行数,列名) //日期
dw_1.GetItemDateTime(行数,列名) //日期时间
dw_1.GetItemDecimal(行数,列名) //小数
```

## 3、行

### 3.1、行处理

```c
dw_1.rowcount() //获取总行 
dw_1.getrow() //获取当前行
dw_1.getSelectedRow(0) //获取已经选择的行
dw_1.setrow(1) //设置当前行
dw_1.scrolltorow(1) //滚动到目标行
dw_1.selectrow(1, true) //将第一行变成选中状态
dw_1.isselected(1) //检查第一行是否被选择

IsRowNew //表达式判断当前行是不是新增的行
IsRowModified() //表达式判断当前行是不是已经修改过
```

### 3.2、高亮显示

```c
//在数据窗口的click事件中，写入以下代码
if row > 0 then
	this.setrow(row)
	this.selectrow(0,false)
	this.selectrow(row,true)
end if
    
//在数据窗口rowfocuschanged事件中，写入以下代码
if currentrow > 0 then
	this.scrolltorow(currentrow)
	this.selectRow(0, false)
	this.selectRow(currentrow,true)
end if
```

### 3.3、隔行换色

```c
//打开数据窗口，选中 detail 区域，找到右侧 color 属性右侧的小图标，在表达式中输入以下代码
if(mod(getrow(),2)=1,rgb(255,255,255),rgb(235,255,235))
```

### 3.4、行序号

添加计算列，在表达式输入框中填写 `getrow()` 即可

### 3.5、移动行

语法：dw_name.rowsmove(开始行,结束行,缓冲区,要移动到的另一窗口名,在哪一行前面插入,插入哪个缓冲区)

缓冲区可选参数：主缓冲区（ primary! ）、删除缓冲区（delete!）、过滤缓冲区（filter!）

```c
//例子：从【删除缓冲区】移动行到【主缓冲区】
dw_1.deleterow(1) //删除第1行 
dw_1.rowsmove(1,dw_1.deletedcount(),delete!,dw_1,1,primary!) //恢复第1行
```

### 3.6、复制行

语法：dw_name.rowscopy(开始行,结束行,缓冲区,要复制到的另一窗口名,在哪一行前面插入,插入哪个缓冲区)

```c
//例1：从【删除缓冲区】复制行到【主缓冲区】
dw_1.deleterow(1) //删除第1行
dw_1.rowscopy(1,1,delete!,dw_1,1,primary!) //复制第1行

//例2：将 dw_2 数据窗口【主缓冲区】中的所有行赋值到 dw_1 数据窗口【主缓冲区】
dw_2.rowscopy(1,dw_2.rowcount(),primary!,dw_1,1,primary!)
```

### 3.7、只读行

```c
//例1：循环设置当前字段每一行的只读状态
long row
for row = 1 to dw_1.rowcount()
	dw_1.Modify("字段名.Protect='" + string(row) + "~tif(表示只读状态字段=1, 1,0)'")
    //控制行为空时只读
    //dw_2.Modify("字段名.Protect='~" + string(row) + "~tif( len( String( trim( 字段名 ) ) ) < 1 or isNull( 字段名 ), 0, 1 )'")
next

//例2：直接设置当前字段所有行的状态
dw_2.modify("字段名.protect=~"0~tif(表达式字段名='表达式字段值' ,1,0)~"")
```

## 4、列

### 4.1、获取数据窗口的列名

```c
integer li_index
for li_index = 1 to integer(dw_1.object.datawindow.column.count)
	messagebox(string(li_index),dw_1.describe("#" + string(li_index) + ".name"))
next
```

### 4.2、获取字段对应的 dbname 名称

```c
integer li_index
for li_index = 1 to integer(dw_1.object.datawindow.column.count)
	messagebox(string(li_index),dw_1.describe("#" + string(li_index) + ".dbname"))
next
```

### 4.3、复制列

```c
dw_1.object.字段名.primary = dw_1.object.字段名.primary
```

### 4.4、获取显示列

```
describe('DataWindow.Table.GridColumns')
```

### 4.5、设置列为只读

```c
例1、将tab order 设置为0
例2、dw_1.modify("列名.tabsequence = 0  ")
例3、dw_1.modify( "列名.edit.displayonly=yes")
例4、dw_1.modify("列名.protect= 1")
例5、设置列为只读并设置列颜色
    dw_1.modify("列名.tabsequence = 0")
    dw_1.modify("列名.background.color='67108864'") //灰色
    dw_1.modify("列名.background.color='16777215'") //白色
```

### 4.6、获取列序号和名称

```c
dw_2.GetColumn()		//获取列序号
dw_2.GetColumnName()	//获取列名称
//注：如果当前列是不可编辑列，那么无法获取到对应的列序号与名称。
```

### 4.7、选中当前列同时选择内容

```c
this.setcolumn('字段名')
this.selecttext(1,this.SelectTextAll())
this.setfocus()
```

## 5、过滤

```c
//例1：按列名过滤
dw_1.setfilter("isnull(列名)")
dw_1.filter()

//例2：按条件过滤
dw_1.setfilter("列名='" + 列值 + "'")
dw_1.filter()
    
//例3：清除过滤
//方式1：
dw_1.setfilter('')
dw_1.filter()
//方式2：
dw_1.setfilter('1=1')
    
//例4：SQL语句过滤
sql = 'select * from 表名 where isnull(字段名 , ''空'') = ''空'' ' ;

//例5：in 过滤
dw_1.setfilter("字段名 in(" + 字段值 + ")")
dw_1.filter()
    
//例6：case 过滤
dw_1.setfilter("case( 字段名 when " + 字段值 + " then 1 else 0 ) = 1 ")
```

## 6、排序

```c
//例1：正序排列
dw_1.setsort("列名 a") //a 可以替换为 asc
dw_1.sort()

//例2：倒序排列
dw_1.setsort("列名 d") //d 可以替换为 desc
dw_1.sort()
    
//例3：双击排序
string ls_old_sort, ls_column
char  lc_sort
if right(dwo.name,2) = '_t' then
	ls_column = left(dwo.name, len(string(dwo.name)) - 2)
	ls_old_sort = this.describe("datawindow.table.sort")
	if ls_column = left(ls_old_sort,  len(ls_old_sort) - 2)  then
		lc_sort = right(ls_old_sort, 1)
		if lc_sort = 'a' then
			lc_sort = 'd'
		else
			lc_sort = 'a'
		end if
		this.setsort(ls_column + " " + lc_sort)
	else
		this.setsort(ls_column + "  a")
	end if
	this.sort()
end if
```

